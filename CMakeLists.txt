cmake_minimum_required(VERSION 3.8)
set_property(GLOBAL PROPERTY USE_FOLDERS ON)
if(NOT DEFINED TELEPORT_SOLUTION_NAME OR "${TELEPORT_SOLUTION_NAME}" MATCHES "")
	if(TELEPORT_BUILD_DOCS)
		project(Teleport_Docs)
		set(VS_STARTUP_PROJECT Sphinx)
		set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT Sphinx)
	elseif(TELEPORT_UNITY)
		project(Teleport_Server_Unity)
	else()
		if(TELEPORT_SERVER)
			project(Teleport_Server)
		else()
			project(Teleport_Client)
		endif()
	endif()
else()
	project("${TELEPORT_SOLUTION_NAME}")
endif()
set(PLATFORM_USE_FMT ON)
set(TELEPORT_SOLUTION_NAME "" CACHE STRING "")
option(TELEPORT_SERVER_SUPPORT_WEBRTC "Should the server support WebRTC connections from browsers?" OFF)
set(TELEPORT_LIBWEBRTC_BUILD_DIR "" CACHE PATH "WebRTC Build path")
set(TELEPORT_PLATFORM_DIR "${CMAKE_SOURCE_DIR}/firstparty/Platform" )
set(SIMUL_PLATFORM_FOLDER_PREFIX "Platform" )
set(SIMUL_PLATFORM_DIR ${TELEPORT_PLATFORM_DIR})
set(PLATFORM_IMGUI ON)
option(TELEPORT_UNITY "Build for Unity?" ${TELEPORT_UNITY})
option(TELEPORT_BUILD_DOCS "Build documentation?" OFF)
option(TELEPORT_INTERNAL_CHECKS "Internal checks for development?" OFF)
if(TELEPORT_UNITY)
	set(TELEPORT_SERVER ON CACHE BOOL "")
else()
	option(TELEPORT_SERVER "Build for server? If true, client will not be built." ${TELEPORT_SERVER})
endif()
if(TELEPORT_SERVER OR TELEPORT_BUILD_DOCS)
	set(TELEPORT_CLIENT OFF)
else()
	set(TELEPORT_CLIENT ON)
	option(TELEPORT_CLIENT_USE_D3D12 "Use D3D12?" OFF)
	if( ${TELEPORT_CLIENT_USE_D3D12})
		set(TELEPORT_CLIENT_USE_D3D11 OFF)
	else()
		set(TELEPORT_CLIENT_USE_D3D11 ON)
	endif()
	option(TELEPORT_CLIENT_USE_PLATFORM_VIDEO_DECODER "Use Platform video decoder? Currently requires TELEPORT_CLIENT_USE_D3D12" OFF)

	if(${TELEPORT_CLIENT_USE_PLATFORM_VIDEO_DECODER})
		if(NOT ${TELEPORT_CLIENT_USE_D3D12})
			message("Warning: Setting TELEPORT_CLIENT_USE_D3D12 true because TELEPORT_CLIENT_USE_PLATFORM_VIDEO_DECODER is true.")
			set(TELEPORT_CLIENT_USE_D3D12 ON)
		endif()
	endif()
endif()

set(TELEPORT_UNIX OFF)
set(TELEPORT_WINDOWS OFF)

if(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
	set(TELEPORT_WINDOWS ON)
endif()

set(CMAKE_CXX_STANDARD 17 )
set(CMAKE_CXX_STANDARD_REQUIRED ON )
set(CMAKE_CXX_EXTENSIONS ON )

# assume built-in pthreads on Linux/MacOS
message("CMAKE_SYSTEM_NAME ${CMAKE_SYSTEM_NAME}")
if(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
	set(CMAKE_THREAD_LIBS_INIT "-lpthread")
	set(CMAKE_HAVE_THREADS_LIBRARY 1)
	set(CMAKE_USE_WIN32_THREADS_INIT 0)
	set(CMAKE_USE_PTHREADS_INIT 1)
	set(THREADS_PREFER_PTHREAD_FLAG ON)
	set(TELEPORT_UNIX ON)
	# fix linkage for lots of libraries:
	set(CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS} -fPIC)
endif()
message("TELEPORT_UNIX ${TELEPORT_UNIX}")

if(TELEPORT_UNITY)
	get_filename_component(PROJECT_DIR ${CMAKE_BINARY_DIR} DIRECTORY)
	set(TELEPORT_UNITY_PLUGINS_DIR "${PROJECT_DIR}/Assets/Teleport/Plugins/x86_64" CACHE PATH "Location to build TeleportServer.dll for use by Unity")
	set(TELEPORT_UNITY_PROJECT_DIR "${PROJECT_DIR}" CACHE PATH "")
	set(TELEPORT_UNITY_EDITOR_DIR "C:/Program Files/Unity/Editor" CACHE PATH "")
	set(TELEPORT_UNITY_EDITOR_EXE "Unity.exe")
	find_program(TELEPORT_UNITY_EDITOR_EXE Unity.exe PATHS ${TELEPORT_UNITY_EDITOR_DIR})
endif()
set (CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "")

#Override srt encryption to be off.
set(ENABLE_ENCRYPTION OFF CACHE BOOL "Enable encryption in SRT")

#Hide Basis Universal STATIC option, and set to opposite of USE_DYNAMIC_RUNTIME.
if(LIBAV_USE_DYNAMIC_RUNTIME)
	set(STATIC OFF CACHE INTERNAL "Static linking for Basis Universal" FORCE)
else()
	set(STATIC ON CACHE INTERNAL "Static linking for Basis Universal" FORCE)
endif()

set(CONFIG_NAME $<$<CONFIG:Debug>:Debug>$<$<CONFIG:Release>:Release>)
#message(STATUS "CONFIG_NAME ${CONFIG_NAME}")

#defaults for srt:
if(TELEPORT_WINDOWS)
	set(CMAKE_SHARED_LINKER_FLAGS_RELEASE "${CMAKE_SHARED_LINKER_FLAGS_RELEASE} /DEBUG /OPT:REF /OPT:ICF")
	SET(CMAKE_STATIC_LINKER_FLAGS_RELEASE "${CMAKE_STATIC_LINKER_FLAGS_RELEASE}")
	set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /DEBUG")
	set(PTHREAD_INCLUDE_DIR ${CMAKE_SOURCE_DIR}/thirdparty/srt/submodules/pthread-win32)
	#set(PTHREAD_LIBRARY ${CMAKE_SOURCE_DIR}/thirdparty/srt/submodules/pthread-win32/bin/x64_MSVC2015.Release/pthread_lib.lib)
	set(PTHREAD_LIBRARY ${CMAKE_SOURCE_DIR}/thirdparty/srt/submodules/pthread-win32/windows/VS2019/bin/Release-Unicode-64bit-x64/pthread_static_lib.lib)
endif()
option(ENABLE_APPS "" OFF)
option(ENABLE_SHARED "" OFF) 
option(ENABLE_STATIC "" ON)

#openssl

set(SSL_EAY_DEBUG ${CMAKE_SOURCE_DIR}/thirdparty/openssl/libssl_MT.lib)
set(SSL_EAY_RELEASE ${CMAKE_SOURCE_DIR}/thirdparty/openssl/libssl_MT.lib)

set(OPENSSL_INCLUDE_DIR ${CMAKE_SOURCE_DIR}/thirdparty/openssl/include)

if(TELEPORT_UNITY)
	set( CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ${TELEPORT_UNITY_PLUGINS_DIR} )
else()
	set( CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin )
endif()

if(TELEPORT_WINDOWS)
	set(CMAKE_CXX_FLAGS_RELEASE "/Od /Ob0 /Zi /DNDEBUG /DNOMINMAX /MT")
	set(CMAKE_CXX_FLAGS_DEBUG "/Zi /Ob0 /Od /RTC1 /DNOMINMAX /MDd")
	set(CMAKE_C_FLAGS_RELEASE "/MT  /O2 /Ob2 /DNDEBUG /DNOMINMAX")
	set(CMAKE_C_FLAGS_DEBUG "/MTd /Zi /Ob0 /Od /RTC1 /DNOMINMAX")
	#someday we will add /fsanitize=address - when MS have fixed all the address bugs in their core libs.)
endif()

if(TELEPORT_UNIX)
	set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -Wno-gnu-anonymous-struct -Wno-nested-anon-types -Wno-unused-parameter -Wno-unused-private-field -Wno-unused-parameter -Wno-unused-value")
	set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -Wno-gnu-anonymous-struct -Wno-nested-anon-types -Wno-unused-parameter -Wno-unused-private-field -Wno-unused-parameter -Wno-unused-value")

endif()
#defaults for basisu
if(TELEPORT_SERVER)
	option(STATIC "" OFF)
else()
	option(STATIC "" ON)
endif()

option(BUILD_AS_LIBRARY "" ON) 

function(set_target_runtime targname rt)
	# Default to statically-linked runtime.
	if("${rt}" STREQUAL "")
		set(rt "static")
	endif()
	#message(STATUS "rt ${rt}")
	if(${rt} STREQUAL "static")
		SET_TARGET_PROPERTIES(${targname} PROPERTIES COMPILE_OPTIONS "/MT$<$<CONFIG:Debug>:d>")
	else()	
		SET_TARGET_PROPERTIES(${targname} PROPERTIES COMPILE_OPTIONS "/MD$<$<CONFIG:Debug>:d>")
	endif()
endfunction()

function(SetTeleportDefaults targname)
	set_target_properties( ${targname} PROPERTIES PREFIX "" )
	set_property(TARGET ${targname} PROPERTY COMPILE_WARNING_AS_ERROR ON)
	add_definitions(-DUNICODE -D_UNICODE)
	if(MSVC)
	# Treat angle-brackets as representing external headers. Minimal warnings for external headers.
		target_compile_options(${targname} PRIVATE /W3 /wd4011 /wd4514 /JMC /external:anglebrackets /external:W0)
		target_compile_definitions(${targname} PRIVATE TELEPORT_STDCALL=__stdcall)
	else()
	#TODO: -Wno-deprecated-copy should be removed when possible.
		target_compile_options(${targname} PRIVATE -Wall -Wextra -pedantic -Werror -Wno-gnu-zero-variadic-macro-arguments -Wno-address-of-packed-member -Wno-sign-compare -Wno-parentheses -Wno-gnu-anonymous-struct -Wno-nested-anon-types -Wno-unused-parameter -Wno-unused-private-field -Wno-unused-parameter -Wno-unused-value)
		# these options are intended to minimize the necessary changes to build on Linux, and should be removed when possible:
		target_compile_options(${targname} PRIVATE -Wno-missing-field-initializers -Wno-unused-variable -Wno-deprecated-copy)

		# TODO: fix basisu compile on Linux, remove when possible.
		target_compile_options(${targname} PRIVATE -Wno-array-bounds -Wno-reorder-ctor)
		# TODO: This is a fix for errors of the form
		# relocation R_X86_64_32S against `.rodata' can not be used when making a shared object; recompile with -fPIC
		target_compile_options(${targname} PRIVATE -fPIC)
		target_compile_definitions(${targname} PRIVATE TELEPORT_STDCALL=)
	endif()
	target_compile_definitions(${targname} PRIVATE CMAKE_BINARY_DIR=${CMAKE_BINARY_DIR} TELEPORT_SUPPORT_AUDIO=$<BOOL:${TELEPORT_SUPPORT_AUDIO}> CMAKE_SOURCE_DIR=${CMAKE_SOURCE_DIR} TELEPORT_INTERNAL_CHECKS=$<BOOL:${TELEPORT_INTERNAL_CHECKS}>)
	#message("target_compile_definitions(${targname} PRIVATE CMAKE_BINARY_DIR=${CMAKE_BINARY_DIR} TELEPORT_SUPPORT_AUDIO=$<BOOL:${TELEPORT_SUPPORT_AUDIO}> CMAKE_SOURCE_DIR=${CMAKE_SOURCE_DIR} TELEPORT_INTERNAL_CHECKS=$<BOOL:${TELEPORT_INTERNAL_CHECKS}>)")
endfunction()

function(add_teleport_static_library targname)
  add_library(${ARGV})
  set_target_runtime(${targname} static)
  SetTeleportDefaults( ${targname} )
endfunction()

function(add_teleport_static_executable targname)
	add_executable(${ARGV})
	#message( STATUS set_target_runtime ${targname})
	set_target_runtime(${targname} static)
	SetTeleportDefaults( ${targname} )
endfunction()

#set_target_properties(zlib PROPERTIES FOLDER thirdparty/zlib)

add_subdirectory(TeleportCore)

if(TELEPORT_SERVER)
	# Server doesn't include Platform, so load fmt separately.
	add_subdirectory(firstparty/Platform/External/fmt)
	set(LIBAV_USE_DYNAMIC_RUNTIME ON CACHE BOOL "")
	set(LIBAV_BUILD_SHARED_LIBS ON CACHE BOOL "")
	set(SA_BUILD_SHARED_LIB ON CACHE BOOL "")
	add_subdirectory(thirdparty/cpp-httplib)
endif()

if(TELEPORT_SERVER OR TELEPORT_BUILD_DOCS)
	add_subdirectory(TeleportServer)
endif()
 
if(TELEPORT_SERVER)
	set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT TeleportServer)
endif()

if(TELEPORT_CLIENT OR TELEPORT_BUILD_DOCS)
	set(OPENSSL_ROOT_DIR ${CMAKE_CURRENT_SOURCE_DIR}/thirdparty/openssl)
	set(OPENSSL_USE_STATIC_LIBS TRUE)
	set(OPENSSL_MSVC_STATIC_RT TRUE)
	add_subdirectory(firstparty/Platform)
	set(PLATFORM_BUILD_SAMPLES OFF)
	add_subdirectory(pc_client)
	add_subdirectory(ClientRender)
	add_subdirectory(TeleportClient)
	add_subdirectory(client/Shaders/DirectX11)
	add_subdirectory(client/Shaders/DirectX12)
	add_subdirectory(client/Shaders/Vulkan)
	set(LIBAV_USE_DYNAMIC_RUNTIME OFF CACHE BOOL "")
	set(LIBAV_BUILD_SHARED_LIBS OFF CACHE BOOL "")
	set(SA_BUILD_SHARED_LIB OFF CACHE BOOL "")
endif()


set(SA_BUILD_SHARED_LIB ${TELEPORT_SERVER})

add_subdirectory(TeleportAudio)
add_subdirectory(libavstream)

if(TELEPORT_CLIENT_USE_D3D12)
	target_compile_definitions(libavstream PRIVATE LIBAV_USE_D3D12=1)
else()
	target_compile_definitions(libavstream PRIVATE LIBAV_USE_D3D12=0)
endif()

add_subdirectory(thirdparty/enet)
set_target_properties( enet PROPERTIES FOLDER thirdparty/enet)
target_compile_options(enet PRIVATE -fPIC)
add_subdirectory(thirdparty/basis_universal)
set_target_properties( basisu PROPERTIES FOLDER thirdparty/basis_universal)
# TODO: fix basis compile on Linux, remove when possible.

if(TELEPORT_UNIX)
	target_compile_options(basisu PRIVATE -Wno-array-bounds -Wno-reorder-ctor)
endif()

add_subdirectory(thirdparty/draco)
# Here we will put all the draco targets into the thirdparty/draco solution folder:
macro(get_all_targets_recursive targets dir)
    get_property(subdirectories DIRECTORY ${dir} PROPERTY SUBDIRECTORIES)
    foreach(subdir ${subdirectories})
        get_all_targets_recursive(${targets} ${subdir})
    endforeach()

    get_property(current_targets DIRECTORY ${dir} PROPERTY BUILDSYSTEM_TARGETS)
    list(APPEND ${targets} ${current_targets})
endmacro()
set(targets)
get_all_targets_recursive(targets ${CMAKE_CURRENT_SOURCE_DIR}/thirdparty/draco)
foreach(targ ${targets})
	set_target_properties( ${targ} PROPERTIES FOLDER thirdparty/draco)
	#message(${targ})
endforeach()

add_subdirectory(thirdparty/srt)
target_compile_options(srt_virtual PRIVATE -fPIC)
target_compile_options(srt_static PRIVATE -fPIC)
if(TELEPORT_CLIENT OR TELEPORT_BUILD_DOCS)
	set_target_properties( srt_static PROPERTIES FOLDER thirdparty/srt)
endif()
set_target_properties( srt_virtual PROPERTIES FOLDER thirdparty/srt)


if(TELEPORT_BUILD_DOCS)
	add_subdirectory ("docs")
endif()
